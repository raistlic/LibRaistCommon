import org.gradle.api.XmlProvider
import org.gradle.api.artifacts.maven.MavenDeployment

apply plugin: 'maven'
apply plugin: 'signing'

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
    def signingKeyId = findProperty("signingKeyId")
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKeyId, signingKey, signingPassword)
}

uploadArchives {
    repositories {
        mavenDeployer {

            final String username = project.properties['maven.username']
            final String password = project.properties['maven.password']

            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: username, password: password)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: username, password: password)
            }

            pom.project {
                name 'Raistlic\'s Commons Library'
                packaging 'jar'
                description 'Common utilities to make general tasks in Java programs easier.'
                url 'https://github.com/raistlic'

                scm {
                    url 'https://github.com/raistlic/raistlic-lib-commons-core'
                    connection 'https://github.com/raistlic/raistlic-lib-commons-core'
                    developerConnection 'https://github.com/raistlic/raistlic-lib-commons-core'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'leic'
                        name 'Lei Chen'
                        email 'raistlic@gmail.com'
                    }
                }
            }
        }
    }
}